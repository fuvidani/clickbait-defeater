buildscript {
    ext.kotlin_version = '1.2.51'
    ext.spring_version = '2.0.3.RELEASE'
    ext.springCloudVersion = 'Finchley.RELEASE'
    ext.reactorAddonsVersion = '3.1.6.RELEASE'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
    }
}
plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC7-2"
    id "org.sonarqube" version "2.6.2"
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url = 'https://repo.spring.io/libs-milestone'
            url = 'https://repo.spring.io/milestone'
        }
    }

    buildscript {
        ext.kotlin_version = '1.2.51'
        ext.spring_version = '2.0.3.RELEASE'
        repositories {
            jcenter()
            mavenCentral()
        }
    }
    version = "0.0.1-SNAPSHOT"
    group = 'com.clickbait.defeater'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:0.25.0"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        compile 'org.springframework.boot:spring-boot-starter-webflux'
        compile 'org.springframework.boot:spring-boot-starter-actuator'
        compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
        compile 'com.google.code.gson:gson:2.8.2'
        compile "io.projectreactor.addons:reactor-adapter:$reactorAddonsVersion"
        compile "io.projectreactor.addons:reactor-extra:$reactorAddonsVersion"
        compile 'org.hibernate.validator:hibernate-validator:6.0.9.Final'
        compile 'org.glassfish:javax.el:3.0.0'
        compile 'javax.el:javax.el-api:3.0.0'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile "io.projectreactor:reactor-test:$reactorAddonsVersion"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        //noinspection GroovyAssignabilityCheck
        args "src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        //noinspection GroovyAssignabilityCheck
        args "-F", "src/**/*.kt"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    check.dependsOn jacocoTestReport

    detekt {
        version = "1.0.0.RC8"
        defaultProfile {
            input = file("src/main/kotlin")
            filters = ".*/resources/.*,.*/build/.*"
            config = "$rootProject.projectDir/default-detekt-config.yml"
            output = file("build/reports/detekt")
            outputName = "detekt"
            baseline = file("${project.buildDir}/reports/detekt/baseline.xml")
        }
    }
}
